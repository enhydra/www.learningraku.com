<!--
title: Infinite, cycling sequences
link: http://www.learningraku.com/2018/03/02/infinite-cycling-sequences/
author: brian
post_id: 578
date: 2018-03-02 17:32:09
post_name: infinite-cycling-sequences
status: publish
type: post
categories: sequences whatever
tags: 
-->


My <a href="https://www.metacpan.org/module/Tie::Cycle">Tie::Cycle</a> Perl 5 module is almost trivial but it solved a customer problem a long, long time ago. It's makes a list that looks like a scalar. Every time you access the scalar you get the next value in the list (wrapping around at the end).

How would I do the same thing in Perl 6? I started thinking about a way to make a sequence whose elements were always <code>True</code>, infinitely and forever. I fell back on the idea of cycling through a list:

<pre class="brush:plain">
my $colors := make-cycle( &lt;red green blue> );

for $colors {
	put $_;
	last if $++ > 5;
	}

sub make-cycle ( *@a --> Seq ) {
	{ @a[ $++ % * ] } ... Inf
	}
</pre>

It loops through all of the colors:

<pre class="brush:plain">
red
green
blue
red
green
blue
red
</pre>

I could have done the same thing with other lists that would cycle forever. Even the trivial list of one item might be useful. This is what I wanted when I started playing with this:

<pre class="brush:plain">
my $false       := make-cycle( False );
my $true        := make-cycle( True  );
my $true-false  := make-cycle( True, False );
my $false-true  := make-cycle( False, True );
</pre>

The <code>...</code> is the <a href="https://docs.perl6.org/language/operators#infix_...">sequence operator</a>. You can use some code to decide what comes next. That includes deciding what comes first. I want to grab the next element of the array. That's easy. Keep a cursor of the index and add to each each time I grab to something. When it gets too big reset it to the beginning.

Perl 6 has nice syntax for this. All that is in <code>$++ % *</code> that I use for the index in <code>@a[  ]</code>. I need a variable to act as the cursor. I don't need it anywhere else than inside the <a href="https://docs.perl6.org/language/operators#postcircumfix_[_]">array indexing operator</a>.

The <code>$</code> by itself is an anonymous scalar variable that allows you to skip the declaration; the variable shows up exactly once because that's how many times I need it. The <code>++</code> is the auto-increment operator to move the cursor along.

The value in <code>$</code> eventually passes the last index of the list. The modulus operator <code>%</code> brings it back in that range. For a list of 5 items, the first item is list is at indices 0, 5, 10, and so on. Those are all effectively 0 modulo the number elements.

The <code>*</code> is the <A href="https://docs.perl6.org/type/Whatever">Whatever</a> star. What that does depends on where it shows up. In the <a href="https://docs.perl6.org/language/operators#postcircumfix_[_]">array indexing operator</a> it's the number of elements (not the last index) in the thingy. That's the same number I need for the modulus.

All together that's the cryptic <code>$++ % *</code>. It's a bit tricky to read but I typically don't start writing code like that unless I've done it before. I do it the long way.

I don't even need the <code>make-cycle</code> factory (although it's convenient to forget how this works). I don't think I'd want this all over my code even if I can do it:

<pre class="brush:plain">
my $seq := { ( &lt;red green blue> )[ $++ % * ] } ... Inf;
</pre>

Even then, <code>Seq.from-loop</code> already does most of the work. I don't need to use <code>...</code> because this keeps going by always calling the block of code to get the next value:

<pre class="brush:plain">
my $seq := Seq.from-loop: { ( &lt;red green blue> )[ $++ % *] };
</pre>

It could be a module but this isn't node.
