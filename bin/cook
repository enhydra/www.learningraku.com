#!perl
use v5.10;
use strict;
use warnings;

use experimental qw(signatures);

use File::Basename;
use File::FindLib 'lib';
use File::Path qw(make_path);
use Local::Template::Utils;
use Mojo::Template::Sandbox;

use Carp qw(carp croak);
use File::Spec::Functions;
use Hash::AsObject;
use Mojo::JSON qw(encode_json);
use Mojo::Log;
use Mojo::Template;
use Mojo::Util qw(dumper);

my $log    = get_logger();
my $config = get_config();

my @items;

sub make_json {
	open my $fh, '>:raw', $config->{items_json} or do {
		$log->error( "Could not open <$config->{items_json}> for writing: $!" );
		exit 1;
		};
	print { $fh } encode_json( \@items );
	close $fh;
	}

my @posts = glob( "_posts/*.html" );
foreach my $file ( @posts ) {
	my( $header, $content ) = split_doc( $file );
	my $local_path = catfile(
		$config->{base_dir}, local_path( $file, $header ), 'index.html'
		);
	my $template_path = catfile( $config->{template_dir}, $config->{template} );
	process_file( $file, $local_path, $template_path );
	}

add_cname();
make_json();

my @pages = glob( "_templates/*.html" );
foreach my $file ( @pages ) {
	my $local_path = catfile( $config->{base_dir}, $file =~ s|_templates/||r );
	process_file( $file, $local_path, $file );
	}

sub add_cname () {
	open my $fh, '>:encoding(UTF-8)', catfile( $config->{base_dir}, 'CNAME' );
	say { $fh } $config->{cname};
	}

sub process_file ( $file, $local_path, $template_path ) {
	$log->debug( "File: <$file>" );
	$log->debug( "Local: <$local_path>" );
	$log->debug( "Template: <$template_path>" );
	Mojo::Template::Sandbox::clear_vars();
	my( $header, $content ) = split_doc( $file );

	my $vars = {
		file               => $file,
		processing_epoch   => time,
		processing_datestr => scalar CORE::localtime,
		post_epoch         => get_epoch_time( $header->{date} ),
		local_path         => $local_path,
		content            => $content,
		web_path           => Mojo::URL->new( $header->{link} )->path,
		config             => $config,
		$header->%*,
		};
	Mojo::Template::Sandbox::set_vars( $vars );

	my $cooked = get_templater()->render_file( $template_path );

	write_page( $local_path, $cooked );

	push @items, do { my %h = $vars->%*; delete $h{content}; \%h };
	}

sub write_page ( $local_path, $content ) {
	make_path( dirname($local_path) );

	open my $fh, '>:encoding(UTF-8)', $local_path;
	say $fh $content;
	close $fh;
	}
